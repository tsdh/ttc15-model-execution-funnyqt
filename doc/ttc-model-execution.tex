\documentclass[submission]{eptcs}
\providecommand{\event}{TTC 2015}

\usepackage[T1]{fontenc}
\usepackage{varioref}
\usepackage{hyperref}

\usepackage{url}
\usepackage{paralist}
\usepackage{graphicx}
\usepackage[cache]{minted}
\newminted{clojure}{fontsize=\fontsize{9}{9},linenos,numbersep=3pt,numberblanklines=false,firstnumber=last}
\newmintinline{clojure}{fontsize=\small}
\newcommand{\code}{\clojureinline}
\VerbatimFootnotes

\title{Solving the TTC Model Execution Case with FunnyQT}
\author{Tassilo Horn
  \institute{Institute for Software Technology, University Koblenz-Landau, Germany}
  \email{horn@uni-koblenz.de}}

\def\titlerunning{Solving the TTC Model Execution Case with FunnyQT}
\def\authorrunning{T. Horn}

\begin{document}
\maketitle

\begin{abstract}
  This paper describes the FunnyQT solution to the TTC 2015 Model Execution
  transformation case.  The solution solves the third variant of the case,
  i.e., it considers and implements the execution semantics of the complete UML
  Activity Diagram language.  The solution won the \emph{most correct solution
    award}.
\end{abstract}


\section{Introduction}
\label{sec:introduction}

This paper describes the FunnyQT\footnote{\url{http://funnyqt.org}}
~\cite{Horn2013MQWFQ,funnyqt-icgt15} solution of the TTC 2015 Model Execution
Case~\cite{model-execution-case-desc}.  It implements the third variant of the
case description, i.e., it implements the execution semantics of the complete
UML Activity Diagram language.  The solution project is available on
Github\footnote{\url{https://github.com/tsdh/ttc15-model-execution-funnyqt}},
and it is set up for easy reproduction on a SHARE image\footnote{The SHARE
  image name is \verb|ArchLinux64_TTC15-FunnyQT_2|}.  The solution has won the
\emph{most correct solution award}.

FunnyQT is a model querying and transformation library for the functional Lisp
dialect Clojure\footnote{\url{http://clojure.org}}.  Queries and
transformations are Clojure programs using the features provided by the FunnyQT
API.

Clojure provides strong metaprogramming capabilities that are used by FunnyQT
in order to define several \emph{embedded domain-specific languages} (DSL) for
different querying and transformation tasks.

FunnyQT is designed with extensibility in mind.  By default, it supports EMF
models and JGraLab TGraph models.  Support for other modeling frameworks can be
added without having to touch FunnyQT's internals.

The FunnyQT API is structured into several namespaces, each namespace providing
constructs supporting concrete querying and transformation use-cases, e.g.,
model management, functional querying, polymorphic functions, relational
querying, pattern matching, in-place transformations, out-place
transformations, bidirectional transformations, and some more.  For solving the
model execution case, only the model management, the functional querying, and
the polymorphic functions APIs have been used.


\section{Solution Description}
\label{sec:solution-description}

The explanations in the case description about the operational semantics on UML
Activity Diagrams suggest an algorithmic solution to the transformation case.
The FunnyQT solution tries to be almost a literal translation of the case
description to Clojure code.

FunnyQT is able to generate metamodel-specific model management APIs.  This
feature has been used here.  The generated API consists of element creation
functions, lazy element sequence functions, attribute access functions, and
reference access functions.  E.g., \code|(a/create-ControlToken!  ad)| creates
a new control token and adds it to the activity diagram model \code|ad|,
\code|(a/isa-Token? x)| returns true if and only if \code|x| is a token,
\code|(a/all-Inputs ad)| returns the lazy sequence of input elements in
\code|ad|, \code|(a/running? n)| and \code|(a/set-running! n true)| query and
set the node \code|n|'s \textsf{running} attribute, and \code|(a/->locals a)|,
\code|(a/->set-locals! a ls)|, \code|(a/->add-locals! a l)|, and
\code|(a/->remove-locals! a l)| query, set, add to, and remove from the
\textsf{locals} reference of the activity \code|a|.

\bigskip{}

In the following, the solution is presented in a top-down manner similar to how
the case description defines the operational semantics of activity diagrams.
The following listing shows the function \code|execute-activity-diagram| which
contains the transformation's main loop.

\begin{clojurecode}
(defn execute-activity-diagram [ad]
  (let [activity (the (a/all-Activities ad))
        trace (a/create-Trace! nil)]
    (a/->set-trace! activity trace)
    (init-variables activity (first (a/all-Inputs ad)))
    (mapc #(a/set-running! % true) (a/->nodes activity))
    (loop [en (first (filter a/isa-InitialNode? (a/->nodes activity)))]
      (when en
        (exec-node en)
        (a/->add-executedNodes! trace en)
        (recur (first (enabled-nodes activity)))))
    trace))
\end{clojurecode}

The function queries the single activity in the diagram, creates a new trace,
and assigns that to the activity.  The activity's variables are initialized and
its nodes are set running.

Then, a \code|loop|-\code|recur| iteration\footnote{\code|loop| is not a loop
  in the sense of Java's \code|for| or \code|while| but a local tail-recursion.
  The \code|loop| declares variables with their initial bindings, and in the
  \code|loop|'s body \code|recur| forms may recurse back to the beginning of
  the \code|loop| providing new bindings for the \code|loop|'s variables.}
performs the actual execution of the activity.  Initially, the variable
\code|en| is bound to the activity's initial node, which gets executed and
added to the trace.  Thereafter, the loop is restarted with the next enabled
node.  Eventually, there won't be an enabled node left, and then the function
returns the trace.

The first step in the execution of an activity is the initialization of its
local and input variables.  The corresponding function \code|init-variables| is
shown in the next listing.  For locals, their current value is set to their
initial value if there is one defined.  For input variables, their current
value is set to the value of the input's corresponding input value element.

\begin{clojurecode}
(defn init-variables [activity input]
  (doseq [lv (a/->locals activity)]
    (when-let [init-value (a/->initialValue lv)]
      (a/->set-currentValue! lv init-value)))
  (doseq [iv (and input (a/->inputValues input))]
    (when-let [val (a/->value iv)]
      (a/->set-currentValue! (a/->variable iv) val))))
\end{clojurecode}

After initializing the variables, the main function sets the activity's nodes
running, and the main loop starts with the activity's initial node.

For different kinds of activity nodes, different execution semantics have to be
encoded.  This is exactly the use-case of FunnyQT's polymorphic functions
(polyfn).  A polymorphic function is declared once, and then different
implementations for instances of different metamodel types can be defined.
When the polyfn is called, a polymorphic dispatch based on the polyfn's first
argument's metamodel type is performed to pick out the right
implementation\footnote{Polyfns support multiple inheritance.  In case of an
  ambiguity during dispatch, e.g., two or more inherited implementations are
  applicable, an error is signaled.}.

The next listing shows the declaration of the polyfn \code|exec-node| and its
implementation for initial nodes.  The declaration only defines the name of the
polyfn and the number of its arguments (just one, here).  The implementation
for initial nodes simply offers one new control token to the initial node's
outgoing control flow edge.\footnote{The FunnyQT function \code|the| is similar
  to Clojure's \code|first| except that it signals an error if the given
  collection contains zero or more than one element.  Thus, it makes the
  assumption that there must be only one outgoing control flow explicit.}

\begin{clojurecode}
(declare-polyfn exec-node [node])

(defn offer-one-ctrl-token [node]
  (let [ctrl-t (a/create-ControlToken! nil)
        out-cf (the (a/->outgoing node))
        offer  (a/create-Offer! nil {:offeredTokens [ctrl-t]})]
    (a/->add-heldTokens! node ctrl-t)
    (a/->add-offers! out-cf offer)))

(defpolyfn exec-node InitialNode [i]
  (offer-one-ctrl-token i))
\end{clojurecode}

The following listing shows the \code|exec-node| implementations for join,
merge, and decision nodes.  Join and Merge nodes simply consume their input
offers and pass the tokens they have been offered on all outgoing control
flows.  Decision nodes act similar but offer their input tokens only on the
outgoing control flow whose guard variable's current value is
true\footnote{\code|(the predicate collection)| returns the single element of
  the collection for which the predicate returns true.  If there is no or more
  elements satisfying the predicate, an error is signaled.}.

\begin{clojurecode}
(defn pass-tokens
  ([n] (pass-tokens n nil))
  ([n out-cf]
   (let [in-toks (consume-offers n)]
     (a/->set-heldTokens! n in-toks)
     (doseq [out-cf (if out-cf [out-cf] (a/->outgoing n))]
       (a/->add-offers!
        out-cf (a/create-Offer!
                nil {:offeredTokens in-toks}))))))

(defpolyfn exec-node JoinNode [jn]
  (pass-tokens jn))

(defpolyfn exec-node MergeNode [mn]
  (pass-tokens mn))

(defpolyfn exec-node DecisionNode [dn]
  (pass-tokens dn (the #(-> % a/->guard a/->currentValue a/value)
                       (a/->outgoing dn))))
\end{clojurecode}

How offers are consumed is defined by the \code|consume-offers| function shown
below.  The offers and their tokens are calculated.  Then, the offered tokens
are divided into control and forked tokens.  For control tokens, their holder
is unset.  For forked tokens, the corresponding base token's holder is unset.
The forked tokens' \textsf{remainingOffersCount} is decremented.  If it has
become zero, the forked token is removed from its holder.  Lastly, the offers
are deleted, and the incoming tokens are returned.

\begin{clojurecode}
(defn consume-offers [node]
  (let [offers    (mapcat a/->offers (a/->incoming node))
        tokens    (mapcat a/->offeredTokens offers)
        ctrl-toks (filter a/isa-ControlToken? tokens)
        fork-toks (filter a/isa-ForkedToken? tokens)]
    (doseq [ct ctrl-toks]
      (a/->set-holder! ct nil))
    (doseq [ft fork-toks]
      (when-let [bt (a/->baseToken ft)]
        (a/->set-holder! bt nil))
      (a/set-remainingOffersCount! ft (dec (a/remainingOffersCount ft)))
      (when (zero? (a/remainingOffersCount ft))
        (a/->set-holder! ft nil)))
    (mapc edelete! offers)
    tokens))
\end{clojurecode}

The remaining kinds of activity nodes are fork nodes, activity final nodes and
opaque actions.  Their \code|exec-node| implementations are printed in the next
listing.

A fork node consumes its offers and creates one forked token per incoming
token.  The incoming tokens are set as the forked tokens' base tokens, and the
remaining offers count is set to the number of outgoing control flows.  All
created forked tokens are offered on each outgoing control flow.

\begin{clojurecode}
(defpolyfn exec-node ForkNode [fn]
  (let [in-toks  (consume-offers fn)
        out-cfs  (a/->outgoing fn)
        out-toks (mapv #(a/create-ForkedToken!
                         nil {:baseToken %, :holder fn,
                              :remainingOffersCount (count out-cfs)})
                       in-toks)]
    (a/->set-heldTokens! fn in-toks)
    (doseq [out-cf out-cfs]
      (a/->add-offers! out-cf (a/create-Offer!
                               nil {:offeredTokens out-toks})))))

(defpolyfn exec-node ActivityFinalNode [afn]
  (consume-offers afn)
  (mapc #(a/set-running! % false)
        (-> afn a/->activity a/->nodes)))

(defpolyfn exec-node OpaqueAction [oa]
  (consume-offers oa)
  (mapc eval-exp (a/->expressions oa))
  (offer-one-ctrl-token oa))
\end{clojurecode}

An activity final node simply consumes all offers and then sets the
\textsf{running} attribute of all nodes contained by the executed activity to
false.  An opaque action also consumes all offers, then evaluates all its
expressions in sequence using the \code|eval-exp| function, and finally offers
one single control token on the outgoing control flow.

How an expression is evaluated depends on (1) its type and (2) on the value of
its \textsf{operator} attribute.  The expression's type is only important in
order to separate unary from binary expressions, and the operator defines the
semantics.  Therefore, the \code|eval-exp| function shown in the next listing
has a special case for boolean unary expressions which negates the expression's
current value using \code|not|.  For all binary expressions, the map
\code|op2fn| mapping from operator enum constants to Clojure functions having
the semantics of that operator is used.  The function determined by looking up
the expression's operator is applied to both operands to compute the new value.

\begin{clojurecode}
(def op2fn {(a/enum-IntegerCalculationOperator-ADD)           +
            (a/enum-IntegerCalculationOperator-SUBRACT)       -
            (a/enum-IntegerComparisonOperator-SMALLER)        <
            (a/enum-IntegerComparisonOperator-SMALLER_EQUALS) <=
            (a/enum-IntegerComparisonOperator-EQUALS)         =
            (a/enum-IntegerComparisonOperator-GREATER_EQUALS) >=
            (a/enum-IntegerComparisonOperator-GREATER)        >
            (a/enum-BooleanBinaryOperator-AND)                #(and %1 %2)
            (a/enum-BooleanBinaryOperator-OR)                 #(or  %1 %2)})

(defn eval-exp [exp]
  (a/set-value! (-> exp a/->assignee a/->currentValue)
                (if (a/isa-BooleanUnaryExpression? exp)
                  (not (-> exp a/->operand a/->currentValue a/value))
                  ((op2fn (a/operator exp))
                   (-> exp a/->operand1 a/->currentValue a/value)
                   (-> exp a/->operand2 a/->currentValue a/value)))))
\end{clojurecode}

After executing all enabled nodes, the transformation's main function
\code|execute-activity-diagram| recomputes the enabled nodes and resumes the
execution.  The enabled nodes are computed by the \code|enabled-nodes| function
shown in the following listing.  The enabled nodes are those nodes of a given
activity which are set running, are no initial nodes\footnote{Initial nodes
  have to be excluded because if they are set running, all of their (zero)
  incoming control flows have offers.}, and receive an offer on each incoming
control flow, or, in the case of a merge node, on one incoming control flow.

\begin{clojurecode}
(defn enabled-nodes [activity]
  (filter (fn [n]
            (and (a/running? n)
                 (not (a/isa-InitialNode? n))
                 ((if (a/isa-MergeNode? n) exists? forall?)
                  #(seq (a/->offers %)) (a/->incoming n))))
          (a/->nodes activity)))
\end{clojurecode}

These 101 NCLOC of algorithmic FunnyQT/Clojure code implement the complete
operational semantics of UML Activity Diagrams (with the exception of data
flows which has not been demanded by the case description).

\section{Evaluation \& Conclusion}
\label{sec:evaluation}

The solution comes with a test suite, and during the official evaluation
achieved a full \emph{correctness} score winning the \emph{most correct
  solution award}.

With 101 lines of non-commented source code, the FunnyQT solution is quite
\emph{concise}.  Of course, \emph{understandability} is a very subjective
measure measure.  The solution should be evident for any Clojure programmer but
even without prior Clojure knowledge, the solution shouldn't be hard to follow
due to the usage of the metamodel-specific API.  Another strong point is that
all steps in the execution of an activity are encoded in one function each
whose definition is almost a literal translation of the English description to
FunnyQT/Clojure code.

The following table shows the \emph{performance} in terms of execution times of
the FunnyQT solution for all provided test models.  These times were measured
on a normal 4-core laptop with 2.6 GHz and 2 GB of RAM dedicated to the JVM.

\vspace{3pt}
\begin{tabular}{|l r | l r | l r |}
  \hline
  \textbf{Model} & \textbf{Time} & \textbf{Model} & \textbf{Time} & \textbf{Model} & \textbf{Time}\\
  \hline
  \emph{test1} & 1.3 ms & \emph{test5} & 0.5 ms & \emph{performance-variant-2} & 1246.5 ms\\
  \emph{test2} & 0.6 ms & \emph{test6 (false)} & 3.7 ms & \emph{performance-variant-3-1} & 1159.6 ms\\
  \emph{test3} & 4.1 ms & \emph{test6 (true)}  & 5.4 ms & \emph{performance-variant-3-2} & 72.7 ms\\
  \emph{test4} & 3.2 ms & \emph{performance-variant-1} & 1104.0 ms & &\\
  \hline
\end{tabular}
\vspace{3pt}

When compared with the reference Java solution, the FunnyQT solution is
slightly faster for all normal and performance test models, and about 8 times
faster for the \emph{performance-variant-3-1} model.

Overall, FunnyQT seems to be very adequate for defining model interpreters.
Especially its polymorphic function facility has been explicitly designed for
these kinds of tasks.


\bibliographystyle{eptcs}
\bibliography{ttc-model-execution}
\end{document}

%%% Local Variables:
%%% mode: latex
%%% TeX-master: t
%%% TeX-command-extra-options: "-shell-escape"
%%% End:

%  LocalWords:  parallelizes
